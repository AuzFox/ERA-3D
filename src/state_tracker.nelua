require("statemachine")

local PIANO_KEYS_COUNT <comptime> = SOUNDCHIP_OCTAVE_SIZE * 2

local Editor = @enum {
    SONG = 0,
    PATTERN,
    INSTRUMENT,
    COUNT
}

local TrackerData = @record {
    clipboard: [SOUNDCHIP_SONG_SIZE]uint8,
    clipboard_len: uinteger,
    clipboard_type: uinteger, -- type of data currently in clipboard
    selection_start: uinteger,
    selection_len: uinteger,
    current_editor: integer,
    octave: uint8,
    song_id: uint8,
    song_x: integer,
    song_y: integer,
    instrument_id: uint8,
    instrument_x: integer,
    instrument_y: integer,
    track: integer,
    pattern_x: integer,
    pattern_y: integer,
    prev_volume: uint8,
    prev_instrument: uint8,
    byte_val: uint8
}

local load_sample_filters: []cstring = {
    "*.wav"
}

local note_names: [SOUNDCHIP_OCTAVE_SIZE]string = {
    "C-",
    "C#",
    "D-",
    "D#",
    "E-",
    "F-",
    "F#",
    "G-",
    "G#",
    "A-",
    "A#",
    "B-"
}

local piano_keys: [PIANO_KEYS_COUNT]cint = {
    -- base octave
    rl.keyboardKey.Z, -- C
    rl.keyboardKey.S, -- C#
    rl.keyboardKey.X, -- D
    rl.keyboardKey.D, -- D#
    rl.keyboardKey.C, -- E
    rl.keyboardKey.V, -- F
    rl.keyboardKey.G, -- F#
    rl.keyboardKey.B, -- G
    rl.keyboardKey.H, -- G#
    rl.keyboardKey.N, -- A
    rl.keyboardKey.J, -- A#
    rl.keyboardKey.M, -- B

    -- octave + 1
    rl.keyboardKey.Q,     -- C
    rl.keyboardKey.TWO,   -- C#
    rl.keyboardKey.W,     -- D
    rl.keyboardKey.THREE, -- D#
    rl.keyboardKey.E,     -- E
    rl.keyboardKey.R,     -- F
    rl.keyboardKey.FIVE,  -- F#
    rl.keyboardKey.T,     -- G
    rl.keyboardKey.SIX,   -- G#
    rl.keyboardKey.Y,     -- A
    rl.keyboardKey.SEVEN, -- A#
    rl.keyboardKey.U      -- B
}

local mouse_in_seqbank_list: boolean = false
local mouse_in_wavbank_list: boolean = false
local data: TrackerData

local function drawLevels(): void
    local l: BKFrame = 0
    local r: BKFrame = 0

    for i = 0, < SOUNDCHIP_FRAMES_PER_UPDATE do
        local idx: uinteger = i * 2
        local ltemp: BKFrame = math.abs(soundchip.aobmem[idx])
        local rtemp: BKFrame = math.abs(soundchip.aobmem[idx + 1])

        if ltemp > l then l = ltemp end
        if rtemp > r then r = rtemp end
    end

    --print("left", l, "right", r)
end

local function drawSong(): void
    gpu:print2D(6, 18, GPU_WHITE, "SEQBANK:")
    local bank_x: int32 = 60
    for i = 0, <= 7 do
        if i + 1 == soundchip.current_seqbank then
            gpu:drawRect2D(bank_x, 18, 12, 9, GPU_ALPHA_GRAY)
            gpu:printHexNibble2D(bank_x + 3, 18, core.cursor_color, i)
        else
            gpu:printHexNibble2D(bank_x + 3, 18, GPU_LGRAY, i)
        end
        bank_x = bank_x + 12
    end
    gpu:print2D(6, 27, GPU_WHITE, "WAVBANK:")
    local bank_x: int32 = 60
    for i = 0, <= 1 do
        if i + 1 == soundchip.current_wavbank then
            gpu:drawRect2D(bank_x, 27, 12, 9, GPU_ALPHA_GRAY)
            gpu:printHexNibble2D(bank_x + 3, 27, core.cursor_color, i)
        else
            gpu:printHexNibble2D(bank_x + 3, 27, GPU_LGRAY, i)
        end
        bank_x = bank_x + 12
    end

    local selcol: uint32 = (data.current_editor == Editor.SONG) and core.cursor_color or GPU_GRAY
    gpu:drawRectLines2D(4, 16, 154, 94, selcol)

    local start_row: int32 = util.midI(0, data.song_y - 3, 248)

    for x = 0, < SOUNDCHIP_TRACK_COUNT do
        for y = 0, < 8 do
            local startx: int32 = 18 + x * 12
            local starty: int32 = 36 + y * 9
            local row: int32
            local byte_val: uint8 = soundchip.song_data[8 * (start_row + y) + x]

            gpu:printHexByte2D(6, starty, GPU_LGRAY, start_row + y)

            if (data.song_x /// 2) == x and data.song_y == (start_row + y) then
                gpu:drawRect2D(startx + ((data.song_x & 1) * 6), starty, 6, 9, selcol)
            end

            local col: uint32 = GPU_PALETTE[x]

            if byte_val == SOUNDCHIP_EMPTY_VALUE then
                gpu:print2D(startx, starty, col, "..")
            else
                gpu:printHexByte2D(startx, starty, col, byte_val)
            end
        end
    end
end

local function getNoteText(note: uint8): string
    if note == SOUNDCHIP_EMPTY_VALUE then
        return "..."
    elseif note == SOUNDCHIP_NOTE_MUTE then
        return "OFF"
    elseif note == SOUNDCHIP_NOTE_RELEASE then
        return "REL"
    else
        local base_note: uint8 = note %%% SOUNDCHIP_OCTAVE_SIZE
        local octave: uint8 = note /// SOUNDCHIP_OCTAVE_SIZE
        return string.format("%s%1X", note_names[base_note], octave)
    end
end

local function drawPatterns(): void
    local start_track: int32 = 0
    local start_pattern_y: int32 = 126

    for i = 0, < 8 do
        local start_pattern_x: int32 = 6 + (i * 13 * 6)

        local selcol: uint32 = (data.current_editor == Editor.PATTERN and data.track == start_track + i) and core.cursor_color or GPU_GRAY
        gpu:drawRectLines2D(start_pattern_x - 2, start_pattern_y - 11, 76, 157, selcol)

        local col: uint32 = GPU_PALETTE[start_track + i]
        gpu:print2D(start_pattern_x + 12, start_pattern_y - 9, col, "TRACK")
        gpu:printHexNibble2D(start_pattern_x + 48, start_pattern_y - 9, col, start_track + i)

        local pattern_id: uint8 = soundchip.song_data[8 * data.song_y + (start_track + i)]

        if pattern_id == SOUNDCHIP_EMPTY_VALUE then
            gpu:print2D(start_pattern_x + 60, start_pattern_y - 9, GPU_GRAY, "..")
        else
            gpu:printHexByte2D(start_pattern_x + 60, start_pattern_y - 9, GPU_LGRAY, pattern_id)
        end

        if pattern_id ~= SOUNDCHIP_EMPTY_VALUE then
            local start_row: int32 = util.midI(0, data.pattern_y - 7, 64-16)
            for j = 0, < 16 do
                local y: int32 = start_pattern_y + j * 9

                if (data.track == start_track + i) and (data.pattern_y == start_row + j) then
                    -- draw cursor
                    local cx: int32
                    local cw: int32 = 6

                    if data.pattern_x == 0 then
                        cx = 0
                        cw = 18
                    else
                        cx = 18 + ((data.pattern_x - 1) * 6)
                    end

                    gpu:drawRect2D(start_pattern_x + 12 + cx, y, cw, 9, selcol)
                end

                local row: *[0]uint8 = soundchip:getPatternRow(pattern_id, start_row + j)
                local note: uint8 = row[0]
                local vol: uint8 = row[1]
                local instr: uint8 = row[2]
                local fx: uint8 = row[3]
                local arg: uint8 = row[4]

                gpu:printHexByte2D(start_pattern_x, y, ((start_row + j) & 0b11 == 0) and GPU_LGRAY or GPU_GRAY, start_row + j)
                gpu:print2D(start_pattern_x + 12, y, GPU_WHITE, getNoteText(note))
                if vol == SOUNDCHIP_EMPTY_VALUE then
                    gpu:print2D(start_pattern_x + 30, y, GPU_GREEN, "..")
                else
                    gpu:printHexByte2D(start_pattern_x + 30, y, GPU_GREEN, vol)
                end
                if instr == SOUNDCHIP_EMPTY_VALUE then
                    gpu:print2D(start_pattern_x + 42, y, GPU_LBLUE, "..")
                else
                    gpu:printHexByte2D(start_pattern_x + 42, y, GPU_LBLUE, instr)
                end
                gpu:print2D(start_pattern_x + 54, y, GPU_PURPULE, ".")
                if fx == SOUNDCHIP_EMPTY_VALUE then
                    gpu:print2D(start_pattern_x + 60, y, GPU_PINK, "..")
                else
                    gpu:printHexByte2D(start_pattern_x + 60, y, GPU_PINK, arg)
                end
            end
        else
            gpu:print2D(start_pattern_x + 18, start_pattern_y + 7 * 9, GPU_LGRAY, "EMPTY")
        end
    end
end

local function drawInstrument(): void
    local selcol: uint32 = (data.current_editor == Editor.INSTRUMENT) and core.cursor_color or GPU_GRAY
    gpu:drawRectLines2D(4, 277, 154, 67, selcol)

    gpu:print2D(6, 279, GPU_WHITE, "INSTRUMENT")

    if data.instrument_id == SOUNDCHIP_EMPTY_VALUE then
        gpu:print2D(71, 279, GPU_GRAY, "..")
    else
        gpu:printHexByte2D(71, 279, GPU_LGRAY, data.instrument_id)

        local label_x: int32 = 6
        local value_x: int32 = 78
        local current_y: int32 = 288
        local idata: *[0]uint8 = soundchip:getInstrument(data.instrument_id)
        local flags: uint8 = idata[0]
        local sample_start: uint32 = ((@uint32)(idata[1]) << 16) | ((@uint32)(idata[2]) << 8) | (@uint32)(idata[3])
        local sample_end: uint32 = ((@uint32)(idata[4]) << 16) | ((@uint32)(idata[5]) << 8) | (@uint32)(idata[6])
        local sustain_start: uint32 = ((@uint32)(idata[7]) << 16) | ((@uint32)(idata[8]) << 8) | (@uint32)(idata[9])
        local sustain_end: uint32 = ((@uint32)(idata[10]) << 16) | ((@uint32)(idata[11]) << 8) | (@uint32)(idata[12])
        local attack: uint8 = idata[13]
        local decay: uint8 = idata[14]
        local sustain: uint8 = idata[15]
        local release: uint8 = idata[16]

        -- draw name background rect
        gpu:drawRect2D(value_x, current_y, 48, 9, GPU_DGRAY)

        -- draw cursor
        switch data.instrument_y do
            case 0 then -- name
                gpu:drawRect2D(value_x + data.instrument_x * 6, 288, 6, 9, selcol)
            case 1 then -- sample mode
                gpu:drawRect2D(value_x, 297, 36, 9, selcol)
            case 2 then -- sample start+end/wavmap id
                gpu:drawRect2D(value_x + ((data.instrument_x /// 6) * 42) + ((data.instrument_x % 6) * 6), 306, 6, 9, selcol)
            case 3 then -- loop mode
                gpu:drawRect2D(value_x, 315, 48, 9, selcol)
            case 4 then -- loop range
                gpu:drawRect2D(value_x + ((data.instrument_x /// 6) * 42) + ((data.instrument_x % 6) * 6), 324, 6, 9, selcol)
            case 5 then -- adsr
                local y: int32 = 324
                if flags & InstrumentFlag.LOOP_MASK == InstrumentFlag.LOOP_RANGE then
                    y = 333
                end
                gpu:drawRect2D(value_x + ((data.instrument_x /// 2) * 18) + ((data.instrument_x & 1) * 6), y, 6, 9, selcol)
        end

        gpu:print2D(label_x, current_y, GPU_GRAY, "NAME")
        local x: int32 = value_x
        for i = 0, < 8 do
            gpu:drawChar2D(idata[17 + i], &x, current_y, {0xFF, 0xFF, 0xFF, 0xFF})
        end
        current_y = current_y + 9

        gpu:print2D(label_x, current_y, GPU_GRAY, "SAMPLE MODE")
        if flags & InstrumentFlag.MANUAL ~= 0 then
            gpu:print2D(value_x, current_y, GPU_WHITE, "MANUAL")
            current_y = current_y + 9

            gpu:print2D(label_x, current_y, GPU_GRAY, "START/END")
            gpu:printHexByte2D(value_x, current_y, GPU_WHITE, (sample_start >> 16) & 0xFF)
            gpu:printHexByte2D(value_x + 12, current_y, GPU_WHITE, (sample_start >> 8) & 0xFF)
            gpu:printHexByte2D(value_x + 24, current_y, GPU_WHITE, sample_start & 0xFF)

            gpu:printHexByte2D(value_x + 42, current_y, GPU_WHITE, (sample_end >> 16) & 0xFF)
            gpu:printHexByte2D(value_x + 54, current_y, GPU_WHITE, (sample_end >> 8) & 0xFF)
            gpu:printHexByte2D(value_x + 66, current_y, GPU_WHITE, sample_end & 0xFF)
        else
            gpu:print2D(value_x, current_y, GPU_WHITE, "WAVMAP")
            current_y = current_y + 9

            gpu:print2D(label_x, current_y, GPU_GRAY, "MAP ID")
            gpu:printHexNibble2D(value_x, current_y, GPU_WHITE, (sample_start >> 8) & 0xF)
            gpu:printHexByte2D(value_x + 6, current_y, GPU_WHITE, sample_start & 0xFF)
        end
        current_y = current_y + 9
    
        gpu:print2D(label_x, current_y, GPU_GRAY, "LOOP MODE")
        switch flags & InstrumentFlag.LOOP_MASK do
            case InstrumentFlag.LOOP_OFF then
                gpu:print2D(value_x, current_y, GPU_WHITE, "OFF")
            case InstrumentFlag.LOOP_FORWARD then
                gpu:print2D(value_x, current_y, GPU_WHITE, "FORWARD")
            case InstrumentFlag.LOOP_PINGPONG then
                gpu:print2D(value_x, current_y, GPU_WHITE, "PINGPONG")
            case InstrumentFlag.LOOP_RANGE then
                gpu:print2D(value_x, current_y, GPU_WHITE, "RANGE")
                current_y = current_y + 9

                local range_color: uint32 = (flags & InstrumentFlag.LOOP_MASK == InstrumentFlag.LOOP_RANGE) and GPU_WHITE or GPU_GRAY
                gpu:print2D(label_x, current_y, GPU_GRAY, "LOOP RANGE")
                gpu:printHexByte2D(value_x, current_y, range_color, (sustain_start >> 16) & 0xFF)
                gpu:printHexByte2D(value_x + 12, current_y, range_color, (sustain_start >> 8) & 0xFF)
                gpu:printHexByte2D(value_x + 24, current_y, range_color, sustain_start & 0xFF)
                
                gpu:printHexByte2D(value_x + 42, current_y, range_color, (sustain_end >> 16) & 0xFF)
                gpu:printHexByte2D(value_x + 54, current_y, range_color, (sustain_end >> 8) & 0xFF)
                gpu:printHexByte2D(value_x + 66, current_y, range_color, sustain_end & 0xFF)
        end
        current_y = current_y + 9
    
        gpu:print2D(label_x, current_y, GPU_GRAY, "ADSR")
        gpu:printHexByte2D(value_x, current_y, GPU_WHITE, attack)
        gpu:printHexByte2D(value_x + 18, current_y, GPU_WHITE, decay)
        gpu:printHexByte2D(value_x + 36, current_y, GPU_WHITE, sustain)
        gpu:printHexByte2D(value_x + 54, current_y, GPU_WHITE, release)
    end
end

local function doNoteInput(): boolean
    local row: *[0]uint8 = soundchip:getPatternRow(soundchip.song_data[8 * data.song_y + data.track], data.pattern_y)

    if input.pressedRepeat(rl.keyboardKey.BACKSPACE) then
        row[0] = SOUNDCHIP_EMPTY_VALUE
        row[1] = SOUNDCHIP_EMPTY_VALUE
        row[2] = SOUNDCHIP_EMPTY_VALUE
        return true
    elseif input.pressedRepeat(rl.keyboardKey.COMMA) then
        row[0] = SOUNDCHIP_NOTE_RELEASE
        row[1] = SOUNDCHIP_EMPTY_VALUE
        row[2] = SOUNDCHIP_EMPTY_VALUE
        return true
    elseif input.pressedRepeat(rl.keyboardKey.PERIOD) then
        row[0] = SOUNDCHIP_NOTE_MUTE
        row[1] = SOUNDCHIP_EMPTY_VALUE
        row[2] = SOUNDCHIP_EMPTY_VALUE
        return true
    else
        for i = 0, < PIANO_KEYS_COUNT do
            if rl.isKeyPressed(piano_keys[i]) then
                if soundchip.audio_state.playing_flags == 0 then
                    local note: uint8 = (@uint8)(i + data.octave * SOUNDCHIP_OCTAVE_SIZE)

                    row[0] = note

                    if row[1] == SOUNDCHIP_EMPTY_VALUE then
                        row[1] = data.prev_volume
                    else
                        data.prev_volume = row[1]
                    end
                    
                    if row[2] == SOUNDCHIP_EMPTY_VALUE then
                        row[2] = data.prev_instrument
                    else
                        data.prev_instrument = row[2]
                    end

                    soundchip:playPreviewNote(note, data.prev_volume, data.prev_instrument)
                    return true
                end
            elseif rl.isKeyReleased(piano_keys[i]) then
                soundchip:stopPreviewNote()
                return false
            end
        end
    end

    return false
end

global function stateTrackerInit(): void
    data.clipboard_len = 0
    data.clipboard_type = 0
    data.selection_start = 0
    data.selection_len = 0
    data.current_editor = Editor.SONG
    data.octave = 4
    data.song_id = 0
    data.song_x = 0
    data.song_y = 0
    data.instrument_id = SOUNDCHIP_EMPTY_VALUE
    data.instrument_x = 0
    data.instrument_y = 0
    data.track = 0
    data.pattern_x = 0
    data.pattern_y = 0
    data.prev_volume = 0xA0
    data.prev_instrument = 0
end

global function stateTrackerEnter(): void
    gpu:setRenderTexture(RenderTexture.TRACKER)
end

global function stateTrackerExit(): void
end

global function stateTrackerUpdate(): void
    if input.is_ctrl_down and rl.isKeyPressed(rl.keyboardKey.I) then
        core:popup(LogEntryType.HOST_ERROR, "sample importing is disabled for now")
        --[[
        soundchip:stopAllTracks()

        local sample_load_path: cstring = tinyfd.openFileDialog(
			"Load Sample",
			core.user_directory,
			#load_sample_filters,
			&load_sample_filters,
			nilptr,
			false
		)

		if sample_load_path ~= nilptr then
            local sample_to_import: rl.wave = rl.loadWave(sample_load_path)

            if not rl.isWaveValid(sample_to_import) then
                core:log(LogEntryType.HOST_ERROR, "failed to open sample file", true)
                return
            end

            if sample_to_import.sampleRate ~= SOUNDCHIP_SAMPLE_RATE or sample_to_import.sampleSize ~= 16 or sample_to_import.channels ~= 2 then
                rl.waveFormat(&sample_to_import, SOUNDCHIP_SAMPLE_RATE, 16, 2)
            end

            if sample_to_import.frameCount > (MEMORY_WAVMEM_SIZE_ELEMENTS /// 2) then
                core:log(LogEntryType.HOST_ERROR, "failed to load sample: file is too large", true)
                rl.unloadWave(sample_to_import)
                return
            end

            core.sample_frame_count = sample_to_import.frameCount
            soundchip:loadSampleDataIntoWAVMEM((@*[0]BKFrame)(sample_to_import.data), core.sample_frame_count)
            rl.unloadWave(sample_to_import)

            statemachine.queued_dialog = DialogID.SAMPLE_CONFIG
		end
        ]]--

        return
    end

    if rl.isKeyPressed(rl.keyboardKey.SPACE) then
        local should_play: boolean = true

        if data.current_editor == Editor.INSTRUMENT and data.instrument_y == 0 then
            should_play = false
        end

        if should_play then
            if soundchip.audio_state.playing_flags ~= 0 then
                soundchip:stopAllTracks()
            else
                soundchip:playSongRow(data.song_y)
            end
            return
        end
    end

    if soundchip.audio_state.playing_flags ~= 0 then
        return
    end

    mouse_in_seqbank_list = input.isMouseInRect(60, 18, 96, 9)
    mouse_in_wavbank_list = input.isMouseInRect(60, 27, 24, 9)
    if mouse_in_seqbank_list and input.left_clicked then
        soundchip:setBankPointers(
            soundchip.current_wavbank,
            SoundchipBank.SEQBANK0 + ((input.mouse_x - 60) /// 12)
        )
    elseif mouse_in_wavbank_list and input.left_clicked then
        soundchip:setBankPointers(
            SoundchipBank.WAVBANK0 + ((input.mouse_x - 60) /// 12),
            soundchip.current_seqbank
        )
    end
    
    if input.pressedRepeat(rl.keyboardKey.MINUS) then
        data.octave = util.midI(0, data.octave - 1, SOUNDCHIP_MAX_OCTAVE)
    elseif input.pressedRepeat(rl.keyboardKey.EQUAL) then
        data.octave = util.midI(0, data.octave + 1, SOUNDCHIP_MAX_OCTAVE)
    end

    switch data.current_editor do
        case Editor.SONG then
            if input.pressedRepeat(rl.keyboardKey.TAB) then
                if input.is_shift_down then
                    data.song_x = util.wrapI(data.song_x - 2, 0, 16)
                    data.track = util.wrapI(data.track - 1, 0, 8)
                else
                    data.song_x = util.wrapI(data.song_x + 2, 0, 16)
                    data.track = util.wrapI(data.track + 1, 0, 8)
                end
            elseif input.pressedRepeat(rl.keyboardKey.PAGE_UP) then
                data.song_y = util.wrapI(data.song_y - 8, 0, 256)
            elseif input.pressedRepeat(rl.keyboardKey.PAGE_DOWN) then
                data.song_y = util.wrapI(data.song_y + 8, 0, 256)
            elseif input.pressedRepeat(rl.keyboardKey.HOME) then
                data.song_y = 0
            elseif input.pressedRepeat(rl.keyboardKey.END) then
                data.song_y = 255
            elseif input.pressedRepeat(rl.keyboardKey.UP) then
                if input.is_alt_down then
                    -- ???
                else
                    data.song_y = util.wrapI(data.song_y - 1, 0, 256)
                end
            elseif input.pressedRepeat(rl.keyboardKey.DOWN) then
                if input.is_alt_down then
                    data.current_editor = Editor.PATTERN
                else
                    data.song_y = util.wrapI(data.song_y + 1, 0, 256)
                end
            elseif input.pressedRepeat(rl.keyboardKey.LEFT) then
                if input.is_alt_down then
                    -- ???
                else
                    if data.song_x & 1 == 0 then
                        data.track = util.wrapI(data.track - 1, 0, 8)
                    end
                    data.song_x = util.wrapI(data.song_x - 1, 0, 16)
                end
            elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                if input.is_alt_down then
                    -- ???
                else
                    if data.song_x & 1 == 1 then
                        data.track = util.wrapI(data.track + 1, 0, 8)
                    end
                    data.song_x = util.wrapI(data.song_x + 1, 0, 16)
                end
            else
                if input.hexEdit(
                    &soundchip.song_data[8 * data.song_y + (data.song_x /// 2)],
                    &data.byte_val,
                    data.song_x & 1,
                    true
                ) then
                    data.song_y = util.wrapI(data.song_y + 1, 0, 256)
                end
            end
        case Editor.PATTERN then
            if input.pressedRepeat(rl.keyboardKey.TAB) then
                if input.is_shift_down then
                    data.song_x = util.wrapI(data.song_x - 2, 0, 16)
                    data.track = util.wrapI(data.track - 1, 0, 8)
                else
                    data.song_x = util.wrapI(data.song_x + 2, 0, 16)
                    data.track = util.wrapI(data.track + 1, 0, 8)
                end
            elseif input.pressedRepeat(rl.keyboardKey.PAGE_UP) then
                data.pattern_y = data.pattern_y - 16
                if data.pattern_y < 0 then
                    data.song_y = util.wrapI(data.song_y - 1, 0, 256)
                end
                data.pattern_y = util.wrapI(data.pattern_y, 0, 64)
            elseif input.pressedRepeat(rl.keyboardKey.PAGE_DOWN) then
                data.pattern_y = data.pattern_y + 16
                if data.pattern_y > 63 then
                    data.song_y = util.wrapI(data.song_y + 1, 0, 256)
                end
                data.pattern_y = util.wrapI(data.pattern_y, 0, 64)
            elseif input.pressedRepeat(rl.keyboardKey.HOME) then
                data.pattern_y = 0
            elseif input.pressedRepeat(rl.keyboardKey.END) then
                data.pattern_y = 63
            elseif input.pressedRepeat(rl.keyboardKey.UP) then
                if input.is_alt_down then
                    data.current_editor = Editor.SONG
                else
                    data.pattern_y = data.pattern_y - 1
                    if data.pattern_y < 0 then
                        data.pattern_y = 63
                        data.song_y = util.wrapI(data.song_y - 1, 0, 256)
                    end
                end
            elseif input.pressedRepeat(rl.keyboardKey.DOWN) then
                if input.is_alt_down then
                    data.current_editor = Editor.INSTRUMENT
                else
                    data.pattern_y = data.pattern_y + 1
                    if data.pattern_y > 63 then
                        data.pattern_y = 0
                        data.song_y = util.wrapI(data.song_y + 1, 0, 256)
                    end
                end
            elseif input.pressedRepeat(rl.keyboardKey.LEFT) then
                if input.is_alt_down then
                    -- ???
                else
                    data.pattern_x = data.pattern_x - 1
                    if data.pattern_x < 0 then
                        data.pattern_x = 7
                        data.song_x = util.wrapI(data.song_x - 2, 0, 16)
                        data.track = util.wrapI(data.track - 1, 0, 8)
                    end
                end
            elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                if input.is_alt_down then
                    -- ???
                else
                    data.pattern_x = data.pattern_x + 1
                    if data.pattern_x > 7 then
                        data.pattern_x = 0
                        data.song_x = util.wrapI(data.song_x + 2, 0, 16)
                        data.track = util.wrapI(data.track + 1, 0, 8)
                    end
                end
            else
                switch data.pattern_x do
                    case 0 then -- note
                        if doNoteInput() then
                            data.pattern_y = util.wrapI(data.pattern_y + 1, 0, 64)
                        end
                    case 1,2,3,4,6,7 then -- vol, inst, arg
                        local nibble_index: uinteger
                        local byte_index: uinteger
                        local row: *[0]uint8 = soundchip:getPatternRow(soundchip.song_data[8 * data.song_y + data.track], data.pattern_y)

                        if data.pattern_x < 6 then
                            nibble_index = (data.pattern_x - 1) & 1
                            byte_index = (data.pattern_x < 3) and 1 or 2
                        else
                            nibble_index = data.pattern_x & 1
                            byte_index = 4
                        end

                        if input.hexEdit(
                            &row[byte_index],
                            &data.byte_val,
                            nibble_index,
                            true
                        ) then
                            if byte_index == 1 then
                                data.prev_volume = data.byte_val
                            elseif byte_index == 2 then
                                data.prev_instrument = data.byte_val
                                data.instrument_id = data.byte_val
                                data.instrument_x = 0
                                data.instrument_y = 0
                            end
                            data.pattern_y = util.wrapI(data.pattern_y + 1, 0, 64)
                        end
                    case 5 then -- fx
                        do end
                end
            end
        case Editor.INSTRUMENT then
            if input.pressedRepeat(rl.keyboardKey.UP) then
                if input.is_alt_down then
                    data.current_editor = Editor.PATTERN
                elseif input.is_shift_down then
                    data.instrument_id = data.instrument_id + 0x10
                    data.instrument_x = 0
                    data.instrument_y = 0
                else
                    data.instrument_x = 0
                    data.instrument_y = util.wrapI(data.instrument_y - 1, 0, 6)
                    if data.instrument_y == 4 then
                        local idata: *[0]uint8 = soundchip:getInstrument(data.instrument_id)
                        if idata[0] & InstrumentFlag.LOOP_MASK ~= InstrumentFlag.LOOP_RANGE then
                            data.instrument_y = data.instrument_y - 1
                        end
                    end
                end
            elseif input.pressedRepeat(rl.keyboardKey.DOWN) then
                if input.is_alt_down then
                    -- ???
                elseif input.is_shift_down then
                    data.instrument_id = data.instrument_id - 0x10
                    data.instrument_x = 0
                    data.instrument_y = 0
                else
                    data.instrument_x = 0
                    data.instrument_y = util.wrapI(data.instrument_y + 1, 0, 6)
                    if data.instrument_y == 4 then
                        local idata: *[0]uint8 = soundchip:getInstrument(data.instrument_id)
                        if idata[0] & InstrumentFlag.LOOP_MASK ~= InstrumentFlag.LOOP_RANGE then
                            data.instrument_y = data.instrument_y + 1
                        end
                    end
                end
            elseif input.is_shift_down and input.pressedRepeat(rl.keyboardKey.LEFT) then
                data.instrument_id = data.instrument_id - 1
                data.instrument_x = 0
                data.instrument_y = 0
            elseif input.is_shift_down and input.pressedRepeat(rl.keyboardKey.RIGHT) then
                data.instrument_id = data.instrument_id + 1
                data.instrument_x = 0
                data.instrument_y = 0
            else
                local idata: *[0]uint8 = soundchip:getInstrument(data.instrument_id)
                local flags: uint8 = idata[0]
                local sample_start: uint32 = ((@uint32)(idata[1]) << 16) | ((@uint32)(idata[2]) << 8) | (@uint32)(idata[3])
                local sample_end: uint32 = ((@uint32)(idata[4]) << 16) | ((@uint32)(idata[5]) << 8) | (@uint32)(idata[6])
                local sustain_start: uint32 = ((@uint32)(idata[7]) << 16) | ((@uint32)(idata[8]) << 8) | (@uint32)(idata[9])
                local sustain_end: uint32 = ((@uint32)(idata[10]) << 16) | ((@uint32)(idata[11]) << 8) | (@uint32)(idata[12])
                local attack: uint8 = idata[13]
                local decay: uint8 = idata[14]
                local sustain: uint8 = idata[15]
                local release: uint8 = idata[16]

                switch data.instrument_y do
                    case 0 then -- name
                        if input.pressedRepeat(rl.keyboardKey.LEFT) then
                            data.instrument_x = util.wrapI(data.instrument_x - 1, 0, 8)
                        elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                            data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 8)
                        else
                            local c: cint = rl.getCharPressed()
                            while c ~= 0 do
                                idata[17 + data.instrument_x] = (@uint8)(c)
                                data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 8)
                                c = rl.getCharPressed()
                            end
                        end
                    case 1 then -- sample mode
                        if input.pressedRepeat(rl.keyboardKey.LEFT) or input.pressedRepeat(rl.keyboardKey.RIGHT) then
                            flags = flags ~ InstrumentFlag.MANUAL
                            idata[0] = flags
                            idata[1] = 0
                            idata[2] = 0
                            idata[3] = 0
                        end
                    case 2 then -- sample start+end/wavmap id
                        if flags & InstrumentFlag.MANUAL ~= 0 then
                            if input.pressedRepeat(rl.keyboardKey.LEFT) then
                                data.instrument_x = util.wrapI(data.instrument_x - 1, 0, 12)
                            elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                                data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 12)
                            else
                                if input.hexEdit(
                                    &idata[1 + data.instrument_x /// 2],
                                    &data.byte_val,
                                    data.instrument_x & 1,
                                    false
                                ) then
                                    data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 12)
                                end
                            end
                        else
                            if input.pressedRepeat(rl.keyboardKey.LEFT) then
                                data.instrument_x = util.wrapI(data.instrument_x - 1, 0, 3)
                            elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                                data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 3)
                            else
                                local byte_index: uinteger
                                local nibble_index: uinteger
                                switch data.instrument_x do
                                    case 0 then
                                        byte_index = 2
                                        nibble_index = 1
                                    case 1 then
                                        byte_index = 3
                                        nibble_index = 0
                                    case 2 then
                                        byte_index = 3
                                        nibble_index = 1
                                end
                                if input.hexEdit(
                                    &idata[byte_index],
                                    &data.byte_val,
                                    nibble_index,
                                    false
                                ) then
                                    if idata[2] > 0x01 then
                                        idata[2] = 0x01
                                    end
                                    data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 3)
                                end
                            end
                        end
                    case 3 then -- loop mode
                        local loop_mode: uint8 = flags & InstrumentFlag.LOOP_MASK
                        if input.pressedRepeat(rl.keyboardKey.LEFT) then
                            loop_mode = util.wrapI(loop_mode - 1, 0, 4)
                            flags = (flags & ~InstrumentFlag.LOOP_MASK) | loop_mode
                            idata[0] = flags
                        elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                            loop_mode = util.wrapI(loop_mode + 1, 0, 4)
                            flags = (flags & ~InstrumentFlag.LOOP_MASK) | loop_mode
                            idata[0] = flags
                        end
                    case 4 then -- loop range
                        if input.pressedRepeat(rl.keyboardKey.LEFT) then
                            data.instrument_x = util.wrapI(data.instrument_x - 1, 0, 12)
                        elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                            data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 12)
                        else
                            if input.hexEdit(
                                &idata[7 + data.instrument_x /// 2],
                                &data.byte_val,
                                data.instrument_x & 1,
                                false
                            ) then
                                data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 12)
                            end
                        end
                    case 5 then -- adsr
                        if input.pressedRepeat(rl.keyboardKey.LEFT) then
                            data.instrument_x = util.wrapI(data.instrument_x - 1, 0, 8)
                        elseif input.pressedRepeat(rl.keyboardKey.RIGHT) then
                            data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 8)
                        else
                            if input.hexEdit(
                                &idata[13 + data.instrument_x /// 2],
                                &data.byte_val,
                                data.instrument_x & 1,
                                false
                            ) then
                                data.instrument_x = util.wrapI(data.instrument_x + 1, 0, 8)
                            end
                        end
                end
            end
    end
end

global function stateTrackerDraw(): void
    gpu:colorMask(GPU_COLOR_MASK_A)
	gpu:clearColor(0)
	gpu:clear(GPU_CLEAR_ALL)
	gpu:colorMask(GPU_COLOR_MASK_ALL)
    gpu:camera2D(-1)

    -- TODO: disable editing when in empty phrase/instrument

    gpu:print2D(250, 18, GPU_GRAY, "PRESS [CTRL+I] TO IMPORT SAMPLE")

    if soundchip.audio_state.playing_flags ~= 0 then
        local ts: *TrackState = soundchip:getTrackState(data.track)
        data.song_y = ts.song_pos
        data.pattern_y = ts.pattern_pos
    end

    --drawLevels()
    drawSong()
    drawPatterns()
    drawInstrument()
end

global function stateTrackerCleanup(): void
end
