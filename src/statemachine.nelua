require("e3d")

global StateID = @enum {
    BOOT = 0,
    MAIN,
    TRACKER,
    RUN,
    COUNT
}

global State <forwarddecl> = @record {}
global StateMachine <forwarddecl> = @record {}

global StateInitFunction: type = @function(*State, *StateMachine, *e3d.EVM): void
global StateFunction: type = @function(*State, *StateMachine, *e3d.EVM): integer

State = @record {
    init: StateInitFunction,
    update: StateFunction,
    draw: StateFunction,
    metadata: pointer -- generic metadata pointer
}

StateMachine = @record {
    states: [StateID.COUNT]State,
    state_id: integer,
    state_ptr: *State,
    queued_state: integer,
    prev_state: integer
}

function StateMachine:initState(vm: *e3d.EVM): void
    self.state_ptr.init(self.state_ptr, self, vm)
end

function StateMachine:updateState(vm: *e3d.EVM): void
    local new_state: integer = self.state_ptr.update(self.state_ptr, self, vm)
    if new_state ~= self.state_id then
        if self.state_id ~= StateID.RUN then
            self.prev_state = self.state_id
        end
        
        self.state_id = new_state
        self.state_ptr = &self.states[self.state_id]
    end
end

function StateMachine:drawState(vm: *e3d.EVM): void
    local new_state: integer = self.state_ptr.draw(self.state_ptr, self, vm)
    if new_state ~= self.state_id then
        if self.state_id ~= StateID.RUN then
            self.prev_state = self.state_id
        end
        
        self.state_id = new_state
        self.state_ptr = &self.states[self.state_id]
    end
end

function StateMachine:init(vm: *e3d.EVM): void
    for i = 0, < StateID.COUNT do
        local st: *State = &self.states[i]
        st.init(st, self, vm)
    end
    
    self.state_id = StateID.MAIN -- start with main state
    self.state_ptr = &self.states[self.state_id]
    self.queued_state = -1
    self.prev_state = self.state_id
end

function StateMachine:setQueuedState(): void
    if self.queued_state ~= -1 then
        self.state_id = self.queued_state
        self.state_ptr = &self.states[self.state_id]
        self.queued_state = -1
    end
end
